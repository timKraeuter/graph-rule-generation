plugins {
    // Publishing
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

group = 'io.github.timKraeuter'
version = '1.0'


repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // XML-marshalling not part of jdk 11 anymore.
    // https://mvnrepository.com/artifact/jakarta.xml.bind/jaxb-api
    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '4.0.1'
    // https://mvnrepository.com/artifact/org.glassfish.jaxb/jaxb-runtime
    implementation("org.glassfish.jaxb:jaxb-runtime:4.0.3")

    // https://mvnrepository.com/artifact/org.eclipse.elk/org.eclipse.elk.core
    implementation 'org.eclipse.elk:org.eclipse.elk.core:0.8.1'
    // https://mvnrepository.com/artifact/org.eclipse.elk/org.eclipse.elk.alg.layered
    implementation 'org.eclipse.elk:org.eclipse.elk.alg.layered:0.8.1'

    // https://mvnrepository.com/artifact/org.hamcrest/hamcrest
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        grg(MavenPublication) {
            from components.java

            pom {
                name.set(project.name)
                description.set("A library for Groove graph transformation rule generation")
                url.set("https://github.com/timKraeuter/graph-rule-generation")
                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://github.com/timKraeuter/graph-rule-generation/blob/master/LICENSE")
                    }
                }
                developers {
                    developer {
                        id.set("tk")
                        name.set("Tim Kr√§uer")
                    }
                }
                scm {
                    url.set(
                            "https://github.com/timKraeuter/graph-rule-generation.git"
                    )
                    connection.set(
                            "scm:git:git://github.com/timKraeuter/graph-rule-generation.git"
                    )
                    developerConnection.set(
                            "scm:git:git://github.com/timKraeuter/graph-rule-generation.git"
                    )
                }
                issueManagement {
                    url.set("https://github.com/timKraeuter/graph-rule-generation/issues")
                }
            }
        }
    }
}

signing {
    sign publishing.publications.grg
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}